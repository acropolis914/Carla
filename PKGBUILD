# Maintainer: acroarch
# Contributor: falkTX <falktx@falktx.com>

pkgname=carla-improved
pkgver=2.5.1.r578.gc7958135
pkgrel=1
pkgdesc="Audio Plugin Host (fork with UI/UX enhancements, quick search, recent projects, and node positioning)"
arch=('x86_64')
url="https://github.com/acropolis914/Carla"
license=('GPL-2.0-or-later')
depends=(
  'alsa-lib'
  'glibc'
  'hicolor-icon-theme'
  'libglvnd'
  'libx11'
  'libxcursor'
  'libxrandr'
  'python-pyqt6'
  'sdl2'
  'qt6-base'
  'qt6-svg'
  'fluidsynth'
  'liblo'
  'file'
  'libsndfile'
)
makedepends=(
  'git'
  'pkgconf'
  'python'
)
optdepends=(
  'jack: for using carla with JACK'
  'lv2-host: for the LV2 plugin'
  'vst-host: for the VST plugin'
  'python-pyliblo: OSC control support'
  'python-rdflib: LADSPA-RDF support'
)
provides=('carla' 'carla-git' 'clap-host' 'dssi-host' 'ladspa-host' 'lv2-host' 'vst3-host' 'vst-host')
conflicts=('carla' 'carla-git')
source=("git+https://github.com/acropolis914/Carla.git#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/Carla"
  git describe --long --tags --abbrev=8 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' || echo "2.6.0.r0"
}

build() {
  cd "$srcdir/Carla"
  make DEFAULT_QT=6
}

package() {
  cd "$srcdir/Carla"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
